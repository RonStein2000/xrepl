grammar org.xrepl.xscript.Xscript with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate xscript "http://www.xrepl.org/xscript" 
 
 
XScript returns xbase::XExpression:
	{XScript}
	(expressions+=XExpression | expressions+=XScriptExpression | expressions+=XVariableDeclaration)*
;

XScriptExpression returns xbase::XExpression: 
	XImport | XEPackageImport
;

 
XPrimaryExpression returns xbase::XExpression:
	XConstructorCall |
	XBlockExpression |
	XSwitchExpression |
	XFeatureCall |
	XLiteral |
	XIfExpression |
	XForLoopExpression |
	XWhileExpression |
	XDoWhileExpression |
	XThrowExpression |
	XReturnExpression |
	XTryCatchFinallyExpression |
	XParenthesizedExpression | 
	XNewEObject
;
 

// temporary syntax 
XNewEObject: 
	'create' type=[ecore::EClass] 
;


  
QualifiedNameWithWildCard :  
	QualifiedName  ('.' '*')?;

XImport: 
	'import' (static?='static' extension?='extension'?)? importedNamespace=QualifiedNameWithWildCard
;

XEPackageImport: 
	'import' nsUri = STRING ('as' name=ID)? 
;
  